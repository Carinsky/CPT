model{

# CPT likelihood
	
	for (j in 1:nsub) { # subject loop
	
	  for (i in 1:nprob[j])	{ # problem loop

      # subjective values
      
	    vLA[i,j] <- pow(LA[j], alpha[j])
	    vHA[i,j] <- pow(HA[j], alpha[j])
	    vLB[i,j] <- pow(LB[j], alpha[j])
	    vHB[i,j] <- pow(HB[j], alpha[j])

	    # decision weights

	    piHA[i,j] <- (delta[j] * (pow(sprobHA[j], gamma[j]))) / (delta[j] * (pow(sprobHA[j], gamma[j])) + pow(sprobLA[j], gamma[j]))
	    piLA[i,j] <- 1-piHA[i,j]
	
	    piHB[i,j] <- (delta[j] * (pow(sprobHB[j], gamma[j]))) / (delta[j] * (pow(sprobHB[j], gamma[j])) + pow(sprobLB[j], gamma[j]))
	    piLB[i,j] <- 1-piHB[i,j]
	
	    # valuations

	    VA[i,j]  <- piHA[i,j] * vHA[i,j] + piLA[i,j] * vLA[i,j]
	    VB[i,j]  <- piHB[i,j] * vHB[i,j] + piLB[i,j] * vLB[i,j]

	    # rescaling valuations (cf. Krefeld-Schwalb et al., 2021; Stewart et al., 2018)

	    reVA[i,j] <- pow(VA[i,j], (1/alpha[j]))
	    reVB[i,j] <- pow(VB[i,j], (1/alpha[j]))

	    # softmax choice rule

	    pB[i,j] <- (1)/(1+exp((-1*rho[j])*(reVB[i,j]-reVA[i,j])))
	    choice[j] ~ dbern(pB[i,j])
	  
	  }
	}

# priors

#prior distributions are defined on the probit scale in the range from -5 to 5. Values need to be transformed to the desired scale (0-1) using the inverse probit function phi(). Procedure has primarily technical reasons. the normal distributions on probit scale correspond to flat priors on the desired scale.

  # hyperparameters

  mu.probit.alpha ~ dnorm(0,1)T(-5,5)
  sigma.probit.alpha ~ dunif(0,10)
  tau.probit.alpha <- pow(sigma.probit.alpha,-2)

  mu.probit.gamma ~ dnorm(0,1)T(-5,5)
  sigma.probit.gamma ~ dunif(0,10)
  tau.probit.gamma <- pow(sigma.probit.gamma,-2)

  mu.probit.delta ~ dnorm(0,1)T(-5,5) 
  sigma.probit.delta ~ dunif(0,10)
  tau.probit.delta <- pow(sigma.probit.delta,-2)

  mu.probit.rho ~ dnorm(0,1)T(-5,5) 
  sigma.probit.rho ~ dunif(0,10)
  tau.probit.rho <- pow(sigma.probit.rho,-2)

  # inverse-probit to obtain values in [0,1]*scale, scale sets prior boundaries
    
  mu.alpha <- phi(mu.probit.alpha) * 2 
  mu.gamma <- phi(mu.probit.gamma) * 2
  mu.delta <- phi(mu.probit.delta) * 5 
  mu.rho <- phi(mu.probit.rho) * 5

  
  # individual parameters
  
  for (j in 1:nsub) {

    probit.alpha[j] ~ dnorm(mu.probit.alpha, tau.probit.alpha)T(-5,5)
    probit.gamma[j] ~ dnorm(mu.probit.gamma, tau.probit.gamma)T(-5,5)
    probit.delta[j] ~ dnorm(mu.probit.delta, tau.probit.delta)T(-5,5)
    probit.rho[j] ~ dnorm(mu.probit.rho, tau.probit.rho)T(-5,5) 
    
    # inverse-probit to obtain values in [0,1]*scale, scale sets prior boundaries
    
    alpha[j] <- phi(probit.alpha[j]) * 2 
    gamma[j] <- phi(probit.gamma[j]) * 2 
    delta[j] <- phi(probit.delta[j]) * 5
    rho[j] <- phi(probit.rho[j]) * 5

  }

}